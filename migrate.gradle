final def JAVASOURCE_FOLDER = 'javasource'

// required for preventing svn: E155004 - errors
gradle.startParameter.maxWorkerCount = 1

allprojects { prj ->

    def migrate_main_java = task('migrate_main_java') {
        def target = 'src/main/java'

        def files = prj.fileTree JAVASOURCE_FOLDER, {
            include '**/*'
            exclude 'tests/com/**/*'
        }

        doLast() {
            moveFiles prj, files, target
        }
    }


    def migrate_main_resources = task('migrate_main_resources') {
        def target = 'src/main/resources/resources'

        def files = prj.name.endsWith('_test') ?
                prj.fileTree("$JAVASOURCE_FOLDER/resources", {
                    include '**/*'
                }):
                prj.fileTree("$JAVASOURCE_FOLDER/resources", {
                    include '**/*'
                    exclude 'tests/**/*'
                })

        doLast() {
            moveFiles prj, files, target
        }
    }


    def migrate_test_java = task('migrate_test_java') {
        def target = 'src/test/java'

        def files = prj.fileTree "$JAVASOURCE_FOLDER/tests", {
            include 'com/**/*'
        }

        doLast() {
            moveFiles prj, files, target

            FileTree testFiles = prj.fileTree target, {
                include '**/*.java'
            }

            replaceToken testFiles.files, /import\s+tests\.com\./,  'import com.'
            replaceToken testFiles.files, /package\s+tests\.com\./, 'package com.'
        }
    }


    def migrate_test_resources = task('migrate_test_resources') {

        onlyIf {!prj.name.endsWith('_test')}

        def target = 'src/test/resources/resources'

        def files = prj.fileTree "$JAVASOURCE_FOLDER/resources/tests", {
            include '**/*'
        }

        doLast() {
            moveFiles prj, files, target
        }
    }


    def migrate_genmodel = task('mirgrate_genmodel') {

        def files = prj.fileTree "model", {
            include '**/*.genmodel'
        }

        doLast() {
            replaceToken files.files, JAVASOURCE_FOLDER, 'src/test/java'
        }
    }


    def migrate_javacc = task('migrate_javacc') {
        doLast() {
            File buildFile = new File(prj.projectDir, 'build.gradle')

            def content = buildFile.text.replaceAll(/javasource(.*\.jjt?)/, {all, path -> "src/main/java$path"})

            buildFile.write content
        }
    }

    def migrate = task('migrate') {
        onlyIf {new File(prj.projectDir, JAVASOURCE_FOLDER).exists()}

        doLast() {
            prj.fileTree(JAVASOURCE_FOLDER).visit { FileTreeElement fte ->
                if(!fte.isDirectory())
                    throw new GradleException("some files still not migrated. i.e: ${fte.relativePath}")
            }

            deleteFiles prj, JAVASOURCE_FOLDER
        }
    }
    migrate_javacc.dependsOn migrate_genmodel
    migrate_genmodel.dependsOn       migrate_main_java
    migrate_main_java.dependsOn      migrate_main_resources
    migrate_main_resources.dependsOn migrate_test_java
    migrate_test_java.dependsOn      migrate_test_resources

    migrate.dependsOn migrate_main_java
    migrate.dependsOn migrate_main_resources
    migrate.dependsOn migrate_test_java
    migrate.dependsOn migrate_test_resources
    migrate.dependsOn migrate_genmodel
    migrate.dependsOn migrate_javacc
}



def moveFiles(Project prj, files, target) {
    files.visit { FileTreeElement fte ->
        if (!fte.isDirectory() || fte.file.listFiles().length == 0) {

            def source = fte.file.absolutePath
                    .replace(prj.projectDir.absolutePath + File.separator, '')
                    .replace(File.separator, '/')

            def dest = "$target/${fte.relativePath.pathString}"

            move prj, source, dest
        }
    }
}


def move(Project prj, source, dest) {

    if (prj.hasProperty('MOCK_SCM')) {
        prj.copy {
            from source
            into dest
        }
        prj.delete source
    } else {
        println "svn move $source $dest"
        prj.exec {
            if (System.getProperty('os.name').toLowerCase().contains('windows')) {
                commandLine 'cmd', '/c', 'svn', 'move', '--parents', source, dest
            } else {
                commandLine 'sh', '-c', 'svn', 'move', '--parents', source, dest
            }
        }
    }
}


def deleteFiles(Project prj, path) {
    if (prj.hasProperty('MOCK_SCM')) {
        prj.delete path
    } else {
        prj.exec {
            if (System.getProperty('os.name').toLowerCase().contains('windows')) {
                commandLine 'cmd', '/c', 'svn', 'delete', path
            } else {
                commandLine 'sh', '-c', 'svn', 'delete', path
            }
        }
    }
}


def replaceToken(Set<File> files, def pattern, def replaceWith) {
    files.each {f ->

        def content = f.text
                .replaceAll(pattern, replaceWith)

        f.write content
    }
}




