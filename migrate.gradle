final def JAVASOURCE_FOLDER = 'javasource'

// required for preventing svn: E155004 - errors
gradle.startParameter.maxWorkerCount = 1

allprojects { prj ->

    task('migrateSrcStructure_main_java') {

        def files = prj.fileTree JAVASOURCE_FOLDER, {
            include '**/*.java'

            if (prj.name.endsWith('_test')) {
                exclude 'tests/com/**/*.java'
            }
            else {
                exclude 'tests/**/*.java'
            }
        }

        doLast() {
            moveFiles prj, files, 'src/main/java'
        }
    }

    task('migrateSrcStructure_test_java') {

        def files = prj.fileTree "$JAVASOURCE_FOLDER", {
            if (prj.name.endsWith('_test')) {
                include 'tests/com/**/*.java'
            }
            else {
                include 'tests/**/*.java'
            }
        }

        doLast() {
            moveFiles prj, files, 'src/test/java'
        }
    }

    task('migrateSrcStructure_main_res') {

        def files = prj.name.endsWith('_test') ?
                prj.fileTree("$JAVASOURCE_FOLDER", {
                    include '**/*'
                    exclude '**/*.java'
                }):
                prj.fileTree("$JAVASOURCE_FOLDER", {
                    include '**/*'
                    exclude '**/*.java'
                    exclude 'resources/tests/**/*'
                })

        doLast() {
            moveFiles prj, files, 'src/main/resources'
        }
    }

    task('migrateSrcStructure_test_res') {

        onlyIf {!prj.name.endsWith('_test')}

        def files = prj.fileTree "$JAVASOURCE_FOLDER", {
            include 'resources/tests/**/*'
        }

        doLast() {
            moveFiles prj, files, 'src/test/resources'
        }
    }

    task('migrate_genmodel') {

        def files = prj.fileTree "model", {
            include '**/*.genmodel'
        }

        doLast() {
            replaceToken files.files, JAVASOURCE_FOLDER, 'src/test/java'
        }
    }

    task('migrate_javacc') {
        File buildFile = new File(prj.projectDir, 'build.gradle')

        onlyIf {buildFile.exists()}

        doLast() {
            def content = buildFile.text.replaceAll(/javasource(.*\.jjt?)/, {all, path -> "src/main/resources$path"})

            buildFile.write content
        }
    }

    task('migrate') {
        dependsOn   migrateSrcStructure_main_java,
                    migrateSrcStructure_test_java,
                    migrateSrcStructure_main_res,
                    migrateSrcStructure_test_res,
                    migrate_genmodel,
                    migrate_javacc

        onlyIf {new File(prj.projectDir, JAVASOURCE_FOLDER).exists()}

        doLast() {
            prj.fileTree(JAVASOURCE_FOLDER).visit { FileTreeElement fte ->
                if(!fte.isDirectory())
                    throw new GradleException("some files still not migrated. i.e: ${fte.relativePath}")
            }

            deleteFiles prj, JAVASOURCE_FOLDER
        }
    }

    def migrate_test_java = task('migrate_test_java') {
        dependsOn migrate

        def files = prj.fileTree "$JAVASOURCE_FOLDER", {
            include 'tests/com/**/*.java'
        }

        doLast() {
            moveFiles prj, files, 'src/test/java'
        }
    }

    task 'renameTestPackages' {
        doLast() {
            FileTree testFiles = prj.fileTree 'src/test/java/tests', {
                include 'com/**/*.java'
            }

            replaceToken testFiles.files, /import\s+tests\.com\./,  'import com.'
            replaceToken testFiles.files, /package\s+tests\.com\./, 'package com.'

            moveFiles prj, testFiles, 'src/test/java'
        }
    }
}

def moveFiles(Project prj, files, target) {
    files.visit { FileTreeElement fte ->
        if (!fte.isDirectory() || fte.file.listFiles().length == 0) {

            def source = fte.file.absolutePath
                    .replace(prj.projectDir.absolutePath + File.separator, '')
                    .replace(File.separator, '/')

            def dest = "$target/${fte.relativePath.pathString}"

            move prj, source, dest
        }
    }
}


def move(Project prj, source, dest) {

    if (prj.hasProperty('MOCK_SCM')) {
        prj.copy {
            from source
            into dest
        }
        prj.delete source
    } else {
        println "svn move $source $dest"
        prj.exec {
            if (System.getProperty('os.name').toLowerCase().contains('windows')) {
                commandLine 'cmd', '/c', 'svn', 'move', '--parents', source, dest
            } else {
                commandLine 'sh', '-c', 'svn', 'move', '--parents', source, dest
            }
        }
    }
}


def deleteFiles(Project prj, path) {
    if (prj.hasProperty('MOCK_SCM')) {
        prj.delete path
    } else {
        prj.exec {
            if (System.getProperty('os.name').toLowerCase().contains('windows')) {
                commandLine 'cmd', '/c', 'svn', 'delete', path
            } else {
                commandLine 'sh', '-c', 'svn', 'delete', path
            }
        }
    }
}


def replaceToken(Set<File> files, def pattern, def replaceWith) {
    files.each {f ->

        def content = f.text
                .replaceAll(pattern, replaceWith)

        f.write content
    }
}




